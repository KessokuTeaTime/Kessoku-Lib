pluginManagement {
    repositories {
        maven { url "https://maven.fabricmc.net/" }
        maven { url "https://maven.architectury.dev/" }
        maven { url "https://files.minecraftforge.net/maven/" }
        gradlePluginPortal()
    }
}

rootProject.name = 'kessoku-lib'

includeWrapper("fabric")
includeWrapper("neo")

includeModule("base") // Base
includeModule("event-base") // Event Base
includeModule("platform") // Platform
includeModule("registry") // Registry
includeModule("lifecycle-events") // Lifecycle Events
includeModule("command") // Command API
includeModule("keybind") // Keybind API
includeModule("config") // Config API
includeModule("entrypoint") // Entrypoint API
includeModule("entity-events") // Entity Events


void includeWrapper(String platform) {
    include("wrapper/$platform")
    project(":wrapper/$platform").name = "$rootProject.name-$platform"
}

void includeModule(String moduleName) {
    includePlatform(moduleName, "common")
    includePlatform(moduleName, "fabric")
    includePlatform(moduleName, "neo")
}

void includePlatform(String moduleName, String platform) {
    include("$moduleName/$platform")
    project(":$moduleName/$platform").name = "$moduleName-$platform"
}