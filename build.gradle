import org.jetbrains.gradle.ext.Gradle

plugins {
    id "java"
    id "maven-publish"
    id "band.kessoku.scripts" apply false
    id "dev.architectury.loom" apply false
    alias libs.plugins.architectury
    alias libs.plugins.shadow apply false
    alias libs.plugins.spotless
    alias libs.plugins.idea.ext
}

architectury {
    minecraft = libs.versions.minecraft.get()
}

idea {
    project {
        settings {
            runConfigurations {
                "Run Client"(Gradle) {
                    taskNames = ["runClient"]
                }
                "Spotless"(Gradle) {
                    taskNames = ["spotlessApply"]
                }
            }
        }
    }
}

subprojects {
    apply plugin: "band.kessoku.scripts"
    apply plugin: "dev.architectury.loom"
    apply plugin: "architectury-plugin"
    apply plugin: "com.diffplug.spotless"
    apply plugin: "maven-publish"

    assemble.dependsOn spotlessApply

    dependencies {
        minecraft(libs.minecraft)
        mappings(loom.layered {
            mappings(variantOf(libs.yarn) { classifier("v2") })
            mappings(libs.yarn.patch)
        })
        implementation("com.google.auto.service:auto-service-annotations:1.1.1")
        annotationProcessor("com.google.auto.service:auto-service:1.1.1")

        //Test mod
        testImplementation(project(":base-common"))
        testImplementation(project(":entrypoint-common"))
    }

    spotless {
        java {
            licenseHeaderFile(rootProject.file("HEADER"))
            importOrder('java|javax', '', 'net.minecraft', 'net.fabricmc|net.neoforged')
            removeUnusedImports()
            indentWithSpaces()
            trimTrailingWhitespace()
        }
    }

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21

        toolchain.languageVersion = JavaLanguageVersion.of(21)
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        repositories {
            maven {
                name = "KessokuLib"
                url = "https://maven.cloudsmith.io/kessokuteatime/kessokulib/"

                credentials {
                    username = "tex-true"
                    password = System.getenv("CLOUDSMITH_MAVEN_TOKEN")
                }
            }
        }
    }
}

allprojects {
    apply plugin: "com.diffplug.spotless"
}