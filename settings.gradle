pluginManagement {
    repositories {
        maven { url "https://maven.fabricmc.net/" }
        maven { url "https://maven.architectury.dev/" }
        maven { url "https://maven.neoforged.net/releases/" }
        gradlePluginPortal()
    }
}

rootProject.name = "kessoku-lib"

includeWrapper("fabric")
includeWrapper("neo")

includeModule("base") // Base
includeModule("data") // Data
includeModule("event-base") // Event Base
includeModule("platform") // Platform
includeModule("registry") // Registry
includeModule("lifecycle-events") // Lifecycle Events
includeModule("command") // Command API
includeModule("keybinding") // Keybinding API
includeModule("config") // Config API
includeModule("entrypoint") // Entrypoint API
includeModule("entity-events") // Entity Events
includeModule("runtime-resources") // Runtime Resources

void includeWrapper(String platform) {
    include("wrapper/$platform")
    project(":wrapper/$platform").name = "$rootProject.name-$platform"
}

void includeModule(String moduleName) {
    includePlatform(moduleName, "common")
    includePlatform(moduleName, "fabric")
    includePlatform(moduleName, "neo")
}

void includePlatform(String moduleName, String platform) {
    include("$moduleName/$platform")
    project(":$moduleName/$platform").name = "$moduleName-$platform"
}
include 'runtime-resources'

